// C++ highlighter.

include "g"
include "arrows"

include "components_lib.box"
include "connect_lib.box"


synch	= Window[][
	// Boundaries:
	dx	= 60
        dy	= 45
	bx	= PointList[(-dx/2, 0), (dx/2, 0)]
	by	= PointList[(0, -dy/2), (0, dy/2)]
	
	// Primitives:
	d0	= .Put[dff, bx.Get[1.35]]
	d1	= .Put[dff, bx.Get[1.7]]
	
	// I/O:
	c0	= .Put[sig_in, bx.Get[1.1]+by.Get[1.2]]
	c1	= .Put[sig_in, bx.Get[1.1]+by.Get[1.1]]
	in	= .Put[sig_in, (bx.Get[1.1].x, d0.Get["d"].y)]
	out	= .Put[sig_out, (bx.Get[1.9].x, d1.Get["q"].y)]
	
	// Connections:
	Style[.Border[mywidth*scale, color.black]]
	Color[color.black]
	w	= 2*scale
	p0	= (bx.Get[1.45].x, c1.Get["out"].y)
	\ .Line[w, in.Get["out"], d0.Get["d"]]
	\ .Line[w, d0.Get["q"], d1.Get["d"]]
	\ .Line[w, d1.Get["q"], out.Get["in"]]
	\ .Line[w, c1.Get["out"], p0]
	\ .ConHVH[w, PointList[p0, d1.Get["c"]]]
	\ .ConHVH[w, PointList[c0.Get["out"], d0.Get["c"]]]
	
	// Labels:
	Style[.Border[fwidth, color.black]]
	\ .Text[.Font["Helvetica", 12*scale], color.black,
		"clk0", c0.Get["label"];
		"clk1", c1.Get["label"];
		"D_{in}", in.Get["label"];
		"D_{out}", out.Get["label"];
	]
	
	\ .Show[(bx.Get[1].x, by.Get[1].y+3), (bx.Get[2].x, by.Get[2].y-12)]
 	.Save["pdf", "synchroniser.pdf"]
// 	.Save["eps", "synchroniser.eps"]
]
